no-vdom
--------

A haxelib used for static(in compile time) HTML data building

## Installation

git version:

```bash
# Install dependence(only used in macro/compile time)
git clone https://github.com/R32/css-selector.git css-selector --depth 3
# haxelib dev libname folder
haxelib dev csss css-selector

# Clone this repo to local
git clone https://github.com/R32/no-vdom.git no-vdom --depth 3
# Set haxelib
haxelib dev no-vdom no-vdom
```

## Feature

* Intelligent:

  ![screen shot](demo/demo-3.gif)

* **Zero Performance Loss**, Zero runtime dependency

* IE8+ Support. *Note: Need to add polyfills such as [textContext](http://eligrey.com/blog/post/textcontent-in-ie8)*

* utils:

  Simple `HXX` for building simple Elements.

  ```js
  var title = "hi there";
  var content = "click here";
  var fn = function(){ return "element span"; }
  var div = Nvd.HXX(
    <div>
      <a class="btn" title="{ title }"> LL {{ content }} RR </a>
      <br />
      <span title={title}>{{ fn() }}</span>
    </div>
  );
  document.body.appendChild(div);
  ```

  Verify css query: *(See the GIF above)*

  ```haxe
  // 1. define a "macro function" and put it in "non-js" module/class file
  macro static function one(selector) {
    return Nvd.buildQuerySelector("bin/index.html", selector);
  }

  static function main() {
    var node = one(".hello"); // Can be automatically recognized as XXXElement
    var typo = one(".typo");  // If ".typo" is not found then you'll get an error
  }
  ```

## Demo

all the code below is in [demo](demo/Demo.hx?ts=4)

### hello world

```html
<!-- html fragment in index.html -->
<div class="hello-world">
  <h4>hello world</h4>
</div>
```

**Component**: (use `macro` and `abstract` to define it.)

```haxe
@:build(Nvd.build("index.html", ".hello-world", {
  text: $("h4").textContent,
})) abstract HelloWorld(nvd.Comp) {
}

class Demo {
  static function main() {
    var hi = HelloWorld.ofSelector(".hello-world");
    hi.text = "你好, 世界!";
    trace(hi.text);
  }
}

// build: haxe -dce full -D analyzer-optimize -lib no-vdom -main Demo -js demo.js
```

Output:

```js
// Generated by Haxe 4.0.0-rc.2+d415a7d8f
(function () { "use strict";
var Demo = function() { };
Demo.main = function() {
    var hi = document.querySelector(".hello-world");
    hi.children[0].textContent = "你好, 世界!";
    console.log("Demo.hx:7:",hi.children[0].textContent);
};
Demo.main();
})();
```

#### Syntax

```haxe
/**
@file: Specify an HTML file, Relative to current project.
@root: a css selector which will be queried as **root DOMElement** for the Component.
@defs: Specify property binding, for example:
  {
    btn :   $("button"),
    text:   $("label").textContext,
    value:  $("input[type=button]").attr.value,
    x:      $(null).style.left,  // if selector is null/"" then self(root DOMElement).
    y:      $(null).style.top,
  }
*/
Nvd.build(file: String, root: String, ?defs: Dynamic<Defines>);

/**
@selector: a css selector that used to specify a child DOMElement , if null it's represented as root DOMElement.
@keep: Optional, if true that will keep the "css-selector" in output.
*/
function $(selector:String, ?keep: Bool):AUTO;

/**
There are 4 types available: DOMElement/Property/Attribute/Style-Property
  $(...)              => DOMElement
  $(...).XXX          => Property
  $(...).attr.XXX     => Attribute,
  $(...).attr["XXX"]  => Attribute,
  $(...).style.XXX    => Style-Property
*/
```

### form data

```html
<div id="login" style="width: 320px; font-size: 14px">
  <div style="clear: both">
    <label for="name" style="float:left;">Name</label>
    <input style="float:right" type="text" name="name" />
  </div>
  <div style="clear: both">
    <label for="email" style="float:left;">Email address</label>
    <input style="float:right" type="email" name="email">
  </div>
  <div style="clear: both">
    <label style="font-size: 12px"><input type="checkbox" /> Remember me </label>
    <label style="font-size: 12px"><input type="radio" name="herpderp" value="herp" checked="checked" /> Herp </label>
    <label style="font-size: 12px"><input type="radio" name="herpderp" value="derp" /> Derp </label>
    <button style="float:right" type="submit">Submit</button>
  </div>
</div>
```

Component:

```hx
@:build(Nvd.build("index.html", "#login", {
  btn:      $("button[type=submit]"),
  name:     $("input[name=name]").value,
  email:    $("input[name=email]").value,
  remember: $("input[type=checkbox]").checked,
  // Note: IE8 does not support the pseudo-selector ":checked"
  herpderp: $("input[type=radio][name=herpderp]:checked", true).value,
})) abstract LoginForm(nvd.Comp) {
  public inline function getData() {
    return {
      name: name,
      email: email,
      remember: remember,
      herpderp: herpderp,
    }
  }
}


class Demo {
  static function main() {
    // login
    var login = LoginForm.ofSelector("#login");
    login.btn.onclick = function() {
      trace(login.getData());
    }
  }
}
```

![screen shot](demo/demo.gif)

![screen shot](demo/demo-2.gif)

output:

```js
// Generated by Haxe 4.0.0 (git build development @ e6f3b7d)
(function () { "use strict";
var Demo = function() { };
Demo.main = function() {
  var login = window.document.querySelector("#login");
  login.children[2].children[3].onclick = function() {
    console.log("Demo.hx:9:",{ name : login.children[0].children[1].value, email : login.children[1].children[1].value, remember : login.children[2].children[0].children[0].checked, herpderp : login.querySelector("input[type=radio][name=herpderp]:checked").value});
  };
};
Demo.main();
})();
```

## CHANGES

* `x.x.x`:
  Added Simple `HXX`
  Added SVG elements support(Only for Query)
* `0.4.0`: added new data binding syntax
* `0.3.3`: discard HXX.parse
